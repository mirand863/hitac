rule spingo:
    input:
        train = "data/train/{dataset}.fasta",
        test = "data/test/{dataset}.fasta",
        spingo = "bin/SPINGO-1.3/dist/64bit/spingo",
        scripts = expand("scripts/{script}",script=config["scripts"])
    output:
        predictions = "results/predictions/{dataset}/spingo.tsv",
        tmpdir = temp(directory("results/temp/{dataset}/spingo"))
    benchmark:
        repeat("results/benchmark/{dataset}/spingo.tsv", config["benchmark"]["repeat"])
    threads:
        config["threads"]
    conda:
        "../envs/spingo.yml"
    shell:
        """
        mkdir -p {output.tmpdir}
        
        python scripts/fasta_utax2spingo.py \
            {input.train} \
            > {output.tmpdir}/db.fa
        
        {input.spingo} \
            -i {input.test} \
            -d {output.tmpdir}/db.fa \
            -p {threads} \
            > {output.tmpdir}/raw
        
        python scripts/spingo2tab.py \
            {output.tmpdir}/raw \
            > {output.predictions}
        """
